plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

task copyResources(type: Copy) {
    group = 'Build'
    if (project.hasProperty('Project.defaultResourcesLanguage')) {
        // TODO In this path neither "app" nor "main" should be hardcoded
        def dir = "$rootDir/app/src/main/res"
        def language = project.property('Project.defaultResourcesLanguage')
        // TODO Make sure the directory and file exist?
        from "$dir/values-$language"
        into "$dir/values"
        include('strings.xml')
    }
}

tasks.matching { it.name.contains('Resources') && it.name != 'copyResources' }.all { task ->
    task.dependsOn copyResources
}

ext {
    BOOST_ROOT = "C:\\Users\\luk20\\boost_1_79_0"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.dndcharacterbuilder"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
    ndkVersion '21.4.7075529'
}

dependencies {
    def room_version = "2.4.2"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    kapt "androidx.room:room-compiler:$room_version"
}
